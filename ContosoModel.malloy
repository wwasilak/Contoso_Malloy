


source: currency_ex is duckdb.table('../Contoso_Malloy/ParquetFiles/currencyexchange.parquet')

source: customer is duckdb.table('../Contoso_Malloy/ParquetFiles/customer.parquet') extend {
    primary_key: CustomerKey
}
    
source: calendar is duckdb.table('../Contoso_Malloy/ParquetFiles//date.parquet') extend {
    primary_key: DateKey
}

source: store is duckdb.table('../Contoso_Malloy/ParquetFiles/store.parquet') extend {
    primary_key: StoreKey
}

source: product is duckdb.table('../Contoso_Malloy/ParquetFiles/product.parquet') extend {
    primary_key: ProductKey
}

source: order_rows is duckdb.table('../Contoso_Malloy/ParquetFiles/orderrows.parquet') extend {
    
    join_one: product with ProductKey

     dimension: 
     Revenue is Quantity * NetPrice
     Cost is Quantity * UnitCost  
}

source: orders is duckdb.table('../Contoso_Malloy/ParquetFiles/orders.parquet') extend {
    primary_key: OrderKey

    rename: OrderDate is DT

    join_many: order_rows on OrderKey = order_rows.OrderKey
    join_one: customer with CustomerKey
    join_one: store with StoreKey
    join_one: calendar on OrderDate = calendar.`Date`
    join_one: currency_ex on CurrencyCode = currency_ex.ToCurrency and OrderDate = currency_ex.`Date`
        where: currency_ex.FromCurrency = 'USD'

    dimension: 
        YearMonth is OrderDate.month
        `Year` is OrderDate.year
        `Month` is month(OrderDate)

    measure:
        Count_Orders is count()
        Total_Cost is order_rows.Cost.sum()
        Total_Sales is order_rows.Revenue.sum()
        Margin is Total_Sales - Total_Cost
        # percent
        Margin_Percent is Margin/Total_Sales



    view: v_year_month is {
        group_by:
        `Year`
        `Month`
        order_by: `Year` desc, `Month` asc
    }

    view: v_category is {
        group_by: Category is order_rows.product.CategoryName  
    }

    view: v_subcategory is {
        group_by: Subategory is order_rows.product.SubCategoryName 
    }

    view: v_metrics_sales is {
        aggregate:
             # number='0.0,,"M"' 
            Total_Sales
            Margin_Percent
    }

    view: sales_by_year is 
        v_year_month 
        + v_metrics_sales

    view: sales_by_category is 
        v_category + v_metrics_sales 
        + {nest: v_subcategory + v_metrics_sales} 
        + {order_by: Total_Sales desc}
    
}


source: sales is duckdb.table('../Contoso_Malloy/ParquetFiles/sales.parquet') extend {

    join_one: customer with CustomerKey
    join_one: store with StoreKey
    join_one: calendar on OrderDate = calendar.`Date`
}